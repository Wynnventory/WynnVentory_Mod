plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

version = "1.3.0-dev.0"
group = project.maven_group

// ─────────────────────────────────────────────────────────────────────────────
// 1) Detect whether we’re running “build” task
// ─────────────────────────────────────────────────────────────────────────────
def isBuildInvocation = gradle.startParameter.taskNames.any { it == 'build' }

// ─────────────────────────────────────────────────────────────────────────────
// 2) Grab the key only once (env‑var or -PapiKey)
// ─────────────────────────────────────────────────────────────────────────────
def api_key = System.getenv('API_KEY')

// ─────────────────────────────────────────────────────────────────────────────
// 3) Fail fast only if we really need it
// ─────────────────────────────────────────────────────────────────────────────
if (isBuildInvocation && api_key == null) {
	throw new GradleException("""
        You’re running the build task,  
        but no API key was provided!  

        • Set an environment variable:  
          export API_KEY=your‑key  
        • Or pass as a Gradle property:  
          ./gradlew build -PapiKey=your‑key  
        • Or add `apiKey=your‑key` to your ~/.gradle/gradle.properties  
    """.stripIndent())
}

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }
	maven { url = "https://api.modrinth.com/maven" }
	maven { url = "https://maven.parchmentmc.org/" }
	maven { url = "https://maven.fabricmc.net/" }
	maven { url = "https://libraries.minecraft.net/" }
	maven { url = "https://maven.terraformersmc.com/releases" }
	maven { url = "https://maven.shedaniel.me/" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${parchment_version}@zip")
	}

	modImplementation "maven.modrinth:wynntils:v${project.wynntils_version}-fabric"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

  	modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:1.2.1"

	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.2'
	implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.17.2'
	implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.17.2'
}

configurations {
	resolvableImplementation {
		canBeResolved = true
		canBeConsumed = false
		extendsFrom implementation
	}
}

processResources {
	var replaceProperties = [
			java_version			: java_version,
			mod_id					: mod_id,
			mod_version				: project.version,
			minecraft_version_range	: minecraft_version_range,
			loader_version			: loader_version,
			wynntils_version		: wynntils_version,
			modmenu_version			: modmenu_version,
			clothconfig_version		: clothconfig_version,
	]
	inputs.properties replaceProperties
	
	filesMatching("fabric.mod.json") {
	expand replaceProperties
	}

	var apiKeyReplace = [
			api_key : api_key
	]

	inputs.properties apiKeyReplace

	filesMatching("key.dat") {
		expand apiKeyReplace
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

remapJar {
	dependsOn jar

	archiveClassifier.set('remapped')
	inputFile = jar.archiveFile.get().asFile
	sourceNamespace = "named"
	targetNamespace = "intermediary"
}

shadowJar {
	dependsOn remapJar

	from {
		zipTree(remapJar.archiveFile.get().asFile)
	}

	archiveClassifier.set('shadow')
	//relocate 'com.fasterxml.jackson', 'com.wynnventory.shadow.jackson'
	configurations = [project.configurations.resolvableImplementation]
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
			artifact shadowJar.archiveFile.get()
		}
	}

	repositories {
		maven {
			url "file:///${project.projectDir}/mcmodsrepo"
		}
	}
}

build {
	dependsOn shadowJar
}
